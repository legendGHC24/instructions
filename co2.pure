###Pure
Class model::co2
{
    country: String[1];
    year: Integer[1];
    iso_code: String[1];
    population: Integer[1];
    co2: Float[1];
    co2_growth_abs: Float[1];
    co2_growth_prct: Float[1];
}


###Relational
Database local::DuckDuckDatabaseCO2
(
  Table co2
  (
      country VARCHAR(0),
      year BIGINT,
      iso_code VARCHAR(0),
      population BIGINT,
      co2 FLOAT, 
      co2_growth_abs FLOAT,
      co2_growth_prct FLOAT
  )
)


###Mapping
Mapping model::simpleCO2Mapping
(
  *model::co2: Relational
  {
    ~primaryKey
    (
      [local::DuckDuckDatabaseCO2]co2.country,
      [local::DuckDuckDatabaseCO2]co2.year
    )
    country: [local::DuckDuckDatabaseCO2]co2.country,
    year: [local::DuckDuckDatabaseCO2]co2.year,
    iso_code: [local::DuckDuckDatabaseCO2]co2.iso_code,
    population: [local::DuckDuckDatabaseCO2]co2.population,
    co2: [local::DuckDuckDatabaseCO2]co2.co2,
    co2_growth_abs: [local::DuckDuckDatabaseCO2]co2.co2_growth_abs,
    co2_growth_prct: [local::DuckDuckDatabaseCO2]co2.co2_growth_prct
  }
)


###Connection
RelationalDatabaseConnection local::DuckDuckConnectionCO2
{
  type: DuckDB;
  specification: DuckDB
  {
    path: 'co2-duckdb';
  };
  auth: Test;
}


###Runtime
Runtime local::DuckDuckRuntimeCO2
{
  mappings:
  [
    model::simpleCO2Mapping
  ];
  connections:
  [
    local::DuckDuckDatabaseCO2:
    [
      connection: local::DuckDuckConnectionCO2
    ]
  ];
}


###Pure
function model::GetOwidco2CountryNameduck(): meta::pure::tds::TabularDataSet[1]
{
  model::co2.all()->project(
    [
      x | $x.country, 
      x | $x.population, 
      x | $x.year, 
      x | $x.co2
    ],
    ['country', 'population', 'year', 'co2']
  )
  ->from(
    model::simpleCO2Mapping,
    local::DuckDuckRuntimeCO2
  )
}